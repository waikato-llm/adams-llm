# Project: adams
# Date: 2024-08-30 16:27:45
# User: fracpete
# Charset: UTF-8
# Modules: adams-annotator,adams-audio,adams-compress,adams-core,adams-docker,adams-event,adams-excel,adams-ffmpeg,adams-groovy,adams-imaging,adams-imaging-boofcv,adams-imaging-ext,adams-jep,adams-json,adams-math,adams-meta,adams-ml,adams-net,adams-odf,adams-opencv,adams-pdf,adams-pyro4,adams-python,adams-random,adams-rats-core,adams-redis,adams-spreadsheet,adams-terminal,adams-video,adams-visualstats,adams-weka,adams-xml,adams-yaml
#
adams.flow.control.Flow -annotation "Evaluates the model runs across several questions." -flow-execution-listener adams.flow.execution.NullListener -flow-restart-manager adams.flow.control.flowrestart.NullManager
 adams.flow.standalone.CallableActors
  adams.flow.source.SwitchedSource -name reader -condition "adams.flow.condition.bool.Expression -expression \"\\\\\\\"@{ext}\\\\\\\" = \\\\\\\"csv\\\\\\\"\"" -condition "adams.flow.condition.bool.Expression -expression \"\\\\\\\"@{ext}\\\\\\\" = \\\\\\\"xls\\\\\\\"\"" -condition "adams.flow.condition.bool.Expression -expression \"\\\\\\\"@{ext}\\\\\\\" = \\\\\\\"xlsx\\\\\\\"\"" -condition adams.flow.condition.bool.True
   adams.flow.source.SelectObjects -name csv -super-class adams.data.io.input.SpreadSheetReader -initial-objects adams.data.io.input.CsvSpreadSheetReader -initial-objects adams.data.io.input.ExcelSpreadSheetReader -initial-objects adams.data.io.input.ExcelStreamingSpreadSheetReader -non-interactive true
   adams.flow.source.SelectObjects -name xls -super-class adams.data.io.input.SpreadSheetReader -initial-objects adams.data.io.input.ExcelSpreadSheetReader -non-interactive true
   adams.flow.source.SelectObjects -name xslx -super-class adams.data.io.input.SpreadSheetReader -initial-objects adams.data.io.input.ExcelStreamingSpreadSheetReader -non-interactive true
   adams.flow.source.SequenceSource -name invalid
    adams.flow.source.Start
    adams.flow.control.Tee -name stop
     adams.flow.control.Stop -stop-msg "Invalid extension: @{ext}"
  adams.flow.control.Trigger -name "add statistic" -annotation "requires vars: statistic, value"
   adams.flow.source.StorageValue -storage-name stats -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.SpreadSheetInsertRow -no-copy true -after true
   adams.flow.transformer.SpreadSheetSetCell -name statistic -no-copy true -row last -finder adams.data.spreadsheet.cellfinder.CellRange -value @{statistic} -force-string true
   adams.flow.transformer.SpreadSheetSetCell -name value -no-copy true -row last -col 2 -finder adams.data.spreadsheet.cellfinder.CellRange -value @{value}
   adams.flow.transformer.SetStorageValue -storage-name stats
  adams.flow.control.Trigger -name "add min/avg/median/max stats" -annotation "requires variable: statistic_prefix\\nrequires storage: min_avg_median_max"
   adams.flow.source.StorageValue -storage-name min_avg_median_max -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.control.Tee -name min
    adams.flow.transformer.SpreadSheetGetCell
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{statistic_prefix} - min" -expand-value true
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -callable "add statistic"
   adams.flow.control.Tee -name avg
    adams.flow.transformer.SpreadSheetGetCell -col 2
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{statistic_prefix} - avg" -expand-value true
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -callable "add statistic"
   adams.flow.control.Tee -name median
    adams.flow.transformer.SpreadSheetGetCell -col 3
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{statistic_prefix} - median" -expand-value true
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -callable "add statistic"
   adams.flow.control.Tee -name max
    adams.flow.transformer.SpreadSheetGetCell -col 4
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{statistic_prefix} - max" -expand-value true
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -callable "add statistic"
  adams.flow.sink.SpreadSheetDisplay -name Statistics -short-title true -display-type adams.flow.core.displaytype.Default -cell-rendering-customizer adams.gui.core.spreadsheettable.DefaultCellRenderingCustomizer -use-simple-header true -allow-search true -writer adams.data.io.output.NullWriter -preview adams.gui.core.spreadsheetpreview.NullPreview
  adams.flow.control.Tee -name evaluate -annotation "requires var: prefix\\ninput: spreadsheet"
   adams.flow.control.Tee -name "% answers"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index has_answer -reg-exp y" -create-view true
    adams.flow.transformer.SpreadSheetInfo
    adams.flow.transformer.MathExpression -expression "X / @{num_rows} * 100"
    adams.flow.control.Tee -name "add statistic"
     adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{prefix} % has answer" -expand-value true
     adams.flow.transformer.SetVariable -name value -var-name value
     adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
   adams.flow.control.Tee -name "% māori answers"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index has_māori_answer -reg-exp y" -create-view true
    adams.flow.transformer.SpreadSheetInfo
    adams.flow.transformer.MathExpression -expression "X / @{num_rows} * 100"
    adams.flow.control.Tee -name "add statistic"
     adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "@{prefix} % has Māori answer" -expand-value true
     adams.flow.transformer.SetVariable -name value -var-name value
     adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
   adams.flow.control.Tee -name "language quality"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.Invert -row-finder \"adams.data.spreadsheet.rowfinder.MissingValue -att-range language_quality\"" -create-view true
    adams.flow.transformer.SpreadSheetAggregate -aggregate-columns language_quality -aggregate MIN -aggregate AVERAGE -aggregate MEDIAN -aggregate MAX
    adams.flow.transformer.SetStorageValue -storage-name min_avg_median_max
    adams.flow.transformer.SetVariable -var-name statistic_prefix -var-value "@{prefix} language quality" -expand-value true
    adams.flow.transformer.CallableTransformer -callable "add min/avg/median/max stats"
   adams.flow.control.Tee -name "language quality (māori)"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.Invert -row-finder \"adams.data.spreadsheet.rowfinder.MissingValue -att-range language_quality\"" -create-view true
    adams.flow.transformer.SpreadSheetRowFilter -name "SpreadSheetRowFilter (2)" -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index has_māori_answer -reg-exp y" -create-view true
    adams.flow.transformer.SpreadSheetAggregate -aggregate-columns language_quality -aggregate MIN -aggregate AVERAGE -aggregate MEDIAN -aggregate MAX
    adams.flow.transformer.SetStorageValue -storage-name min_avg_median_max
    adams.flow.transformer.SetVariable -var-name statistic_prefix -var-value "@{prefix} language quality (Māori)" -expand-value true
    adams.flow.transformer.CallableTransformer -callable "add min/avg/median/max stats"
   adams.flow.control.Tee -name "answer quality"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.Invert -row-finder \"adams.data.spreadsheet.rowfinder.MissingValue -att-range answer_quality\"" -create-view true
    adams.flow.transformer.SpreadSheetAggregate -aggregate-columns answer_quality -aggregate MIN -aggregate AVERAGE -aggregate MEDIAN -aggregate MAX
    adams.flow.transformer.SetStorageValue -storage-name min_avg_median_max
    adams.flow.transformer.SetVariable -var-name statistic_prefix -var-value "@{prefix} answer quality" -expand-value true
    adams.flow.transformer.CallableTransformer -callable "add min/avg/median/max stats"
   adams.flow.control.Tee -name "answer quality (māori)"
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.Invert -row-finder \"adams.data.spreadsheet.rowfinder.MissingValue -att-range answer_quality\"" -create-view true
    adams.flow.transformer.SpreadSheetRowFilter -name "SpreadSheetRowFilter (2)" -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index has_māori_answer -reg-exp y" -create-view true
    adams.flow.transformer.SpreadSheetAggregate -aggregate-columns answer_quality -aggregate MIN -aggregate AVERAGE -aggregate MEDIAN -aggregate MAX
    adams.flow.transformer.SetStorageValue -storage-name min_avg_median_max
    adams.flow.transformer.SetVariable -var-name statistic_prefix -var-value "@{prefix} answer quality (Māori)" -expand-value true
    adams.flow.transformer.CallableTransformer -callable "add min/avg/median/max stats"
 adams.flow.source.Start
 adams.flow.control.Trigger -name "prompt user"
  adams.flow.standalone.SetVariable -name "restore file" -var-name restore -var-value @{flow_filename_long}.props -value-type FILE_FORWARD_SLASHES -expand-value true
  adams.flow.source.EnterManyValues -name Parameters -stop-if-canceled true -value "adams.flow.source.valuedefinition.FileValueDefinition -name input_file -display Input -help \"the spreadsheet with the results\" -file-chooser-title Input -extension csv -extension xls -extension xlsx -default-value /data/fracpete/waikato/llm/data/rag_eval/llm_context4_01.xlsx -use-forward-slashes true -use-absolute-path true" -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name save_results -display \"Save results?\" -type BOOLEAN -default-value false" -value "adams.flow.source.valuedefinition.FileValueDefinition -name output_file -display Output -help \"the spreadsheet with the statistics\" -file-chooser-title Output -extension csv -default-value /data/fracpete/waikato/llm/data/rag_eval/llm_context4_01-statistics.csv -use-forward-slashes true -use-absolute-path true" -output-type MAP -restoration-enabled true -restoration-file @{restore}
  adams.flow.transformer.MapToVariables
 adams.flow.control.Trigger -name "init stats sheet"
  adams.flow.source.NewSpreadSheet -columns statistic,value -data-row-type adams.data.spreadsheet.DenseDataRow -spreadsheet-type adams.data.spreadsheet.DefaultSpreadSheet
  adams.flow.transformer.SetStorageValue -storage-name stats
 adams.flow.control.Trigger -name "determine reader"
  adams.flow.source.Variable -var-name input_file -conversion adams.data.conversion.StringToString
  adams.flow.transformer.FileExtension
  adams.flow.transformer.SetVariable -var-name ext
  adams.flow.control.Trigger -name reader
   adams.flow.source.CallableSource -callable reader
   adams.flow.transformer.Convert -conversion adams.data.conversion.AnyToCommandline
   adams.flow.transformer.SetVariable -var-name reader
 adams.flow.control.Trigger -name process
  adams.flow.source.Variable -var-name input_file -conversion adams.data.conversion.StringToString
  adams.flow.transformer.SpreadSheetFileReader -reader @{reader}
  adams.flow.transformer.SetStorageValue -storage-name data
  adams.flow.control.Tee -name "determine models"
   adams.flow.transformer.SpreadSheetInfo -output-array true -type COLUMN_VALUES -column-index 1
   adams.flow.control.ArrayProcess
    adams.flow.transformer.StringReplace -find ^model=
    adams.flow.transformer.StringReplace -name "StringReplace (2)" -find --prompt=.*
   adams.flow.transformer.Sort -unique true -comparator adams.core.DefaultCompare
   adams.flow.transformer.SetStorageValue -storage-name models
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_models
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# models"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "determine prompts"
   adams.flow.transformer.SpreadSheetInfo -output-array true -type COLUMN_VALUES -column-index 1
   adams.flow.control.ArrayProcess
    adams.flow.transformer.StringReplace -find ^.*--prompt=
    adams.flow.transformer.StringReplace -name "StringReplace (2)" -find --qna=.*
   adams.flow.transformer.Sort -unique true -comparator adams.core.DefaultCompare
   adams.flow.transformer.SetStorageValue -storage-name prompts
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_prompts
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# prompts"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "determine contexts"
   adams.flow.transformer.SpreadSheetInfo -output-array true -type COLUMN_VALUES -column-index 1
   adams.flow.control.ArrayProcess
    adams.flow.transformer.StringReplace -find ^.*--context=
    adams.flow.transformer.StringReplace -name "StringReplace (2)" -find --.*
   adams.flow.transformer.Sort -unique true -comparator adams.core.DefaultCompare
   adams.flow.transformer.SetStorageValue -storage-name contexts
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_contexts
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# contexts"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "determine rags"
   adams.flow.transformer.SpreadSheetInfo -output-array true -type COLUMN_VALUES -column-index 1
   adams.flow.control.ArrayProcess
    adams.flow.transformer.StringReplace -find ^.*--context=
    adams.flow.transformer.StringReplace -name "StringReplace (2)" -find --run=.*
    adams.flow.transformer.StringReplace -name "StringReplace (3)" -find .*--
   adams.flow.transformer.Sort -unique true -comparator adams.core.DefaultCompare
   adams.flow.transformer.SetStorageValue -storage-name rags
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_rags
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# rags"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "determine questions"
   adams.flow.transformer.SpreadSheetInfo -output-array true -type CELL_VALUES -column-index 4
   adams.flow.transformer.SetStorageValue -storage-name questions
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name num_questions
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# questions"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "# runs"
   adams.flow.transformer.SpreadSheetAggregate -aggregate-columns run -aggregate MAX
   adams.flow.transformer.SpreadSheetGetCell
   adams.flow.transformer.Convert -conversion "adams.data.conversion.MultiConversion -conversion adams.data.conversion.StringToDouble -conversion adams.data.conversion.DoubleToInt"
   adams.flow.transformer.SetVariable -var-name num_runs
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# runs"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name "# rows"
   adams.flow.transformer.SpreadSheetInfo
   adams.flow.transformer.SetVariable -var-name num_total_rows
   adams.flow.control.Tee -name "add statistic"
    adams.flow.transformer.SetVariable -name statistic -var-name statistic -var-value "# evaluations"
    adams.flow.transformer.SetVariable -name value -var-name value
    adams.flow.transformer.CallableTransformer -name "add statistic" -callable "add statistic"
  adams.flow.control.Tee -name global
   adams.flow.transformer.SetVariable -name num_rows -var-name num_rows -var-value @{num_total_rows}
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value global:
   adams.flow.transformer.CallableTransformer -callable evaluate
  adams.flow.control.Trigger -name "per model"
   adams.flow.source.StorageValue -storage-name models -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.SetVariable -name model -var-name model
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value model=@{model}: -expand-value true
   adams.flow.transformer.SetVariable -name regexp -var-name regexp -var-value model=@{model}--.* -expand-value true
   adams.flow.control.Trigger -name evaluate
    adams.flow.source.StorageValue -storage-name data -conversion adams.data.conversion.UnknownToUnknown
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index file -reg-exp @{regexp}" -create-view true
    adams.flow.control.Tee -name "# rows"
     adams.flow.transformer.SpreadSheetInfo
     adams.flow.transformer.SetVariable -name num_rows -var-name num_rows
    adams.flow.transformer.CallableTransformer -callable evaluate
  adams.flow.control.Trigger -name "per prompt"
   adams.flow.source.StorageValue -storage-name prompts -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.SetVariable -name prompt -var-name prompt
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value prompt=@{prompt}: -expand-value true
   adams.flow.transformer.SetVariable -name regexp -var-name regexp -var-value .*--prompt=@{prompt}--.* -expand-value true
   adams.flow.control.Trigger -name evaluate
    adams.flow.source.StorageValue -storage-name data -conversion adams.data.conversion.UnknownToUnknown
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index file -reg-exp @{regexp}" -create-view true
    adams.flow.control.Tee -name "# rows"
     adams.flow.transformer.SpreadSheetInfo
     adams.flow.transformer.SetVariable -name num_rows -var-name num_rows
    adams.flow.transformer.CallableTransformer -callable evaluate
  adams.flow.control.Trigger -name "per context"
   adams.flow.source.StorageValue -storage-name contexts -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.SetVariable -name context -var-name context
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value context=@{context}: -expand-value true
   adams.flow.transformer.SetVariable -name regexp -var-name regexp -var-value .*--context=@{context}--.* -expand-value true
   adams.flow.control.Trigger -name evaluate
    adams.flow.source.StorageValue -storage-name data -conversion adams.data.conversion.UnknownToUnknown
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index file -reg-exp @{regexp}" -create-view true
    adams.flow.control.Tee -name "# rows"
     adams.flow.transformer.SpreadSheetInfo
     adams.flow.transformer.SetVariable -name num_rows -var-name num_rows
    adams.flow.transformer.CallableTransformer -callable evaluate
  adams.flow.control.Trigger -name "per rag"
   adams.flow.source.StorageValue -storage-name rags -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.SetVariable -name rag -var-name rag
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value rag=@{rag}: -expand-value true
   adams.flow.transformer.SetVariable -name regexp -var-name regexp -var-value .*@{rag}--.* -expand-value true
   adams.flow.control.Trigger -name evaluate
    adams.flow.source.StorageValue -storage-name data -conversion adams.data.conversion.UnknownToUnknown
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByValue -att-index file -reg-exp @{regexp}" -create-view true
    adams.flow.control.Tee -name "# rows"
     adams.flow.transformer.SpreadSheetInfo
     adams.flow.transformer.SetVariable -name num_rows -var-name num_rows
    adams.flow.transformer.CallableTransformer -callable evaluate
  adams.flow.control.Trigger -name "per question"
   adams.flow.source.StorageValue -storage-name questions -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.ArrayToSequence
   adams.flow.transformer.SetVariable -name question -var-name question
   adams.flow.transformer.SetVariable -name prefix -var-name prefix -var-value question=@{question}: -expand-value true
   adams.flow.control.Trigger -name evaluate
    adams.flow.source.StorageValue -storage-name data -conversion adams.data.conversion.UnknownToUnknown
    adams.flow.transformer.SpreadSheetRowFilter -finder "adams.data.spreadsheet.rowfinder.ByStringComparison -att-index question -minimum @{question} -minimum-included true -maximum @{question} -maximum-included true" -create-view true
    adams.flow.control.Tee -name "# rows"
     adams.flow.transformer.SpreadSheetInfo
     adams.flow.transformer.SetVariable -name num_rows -var-name num_rows
    adams.flow.transformer.CallableTransformer -callable evaluate
 adams.flow.control.Trigger -name display/save
  adams.flow.source.StorageValue -storage-name stats -conversion adams.data.conversion.UnknownToUnknown
  adams.flow.control.ConditionalTee -name save? -condition "adams.flow.condition.bool.Expression -expression \"@{save_results} = true\""
   adams.flow.sink.SpreadSheetFileWriter -output @{output_file} -writer adams.data.io.output.CsvSpreadSheetWriter
  adams.flow.sink.CallableSink -callable Statistics